*&---------------------------------------------------------------------*
*& Report  ZESZBRNFE_DANFE_NAST
*&
*&---------------------------------------------------------------------*
REPORT  ZESZBRNFE_DANFE_NAST MESSAGE-ID ZESZGRC.

TYPE-POOLS: PBR99, IBCO2.

TABLES: NAST. "Status da mensagem

*** Smart Forms: estrutura de controle
DATA G_CONTROL_PARAMETERS TYPE  SSFCTRLOP.
DATA G_OUTPUT_OPTIONS TYPE  SSFCOMPOP.


*&---------------------------------------------------------------------*
*&      Form  entry
*&---------------------------------------------------------------------*
* Impressão DANFE
*----------------------------------------------------------------------*
FORM ENTRY USING RETURN_CODE US_SCREEN.

  DATA: OTFDATA TYPE TSFOTF.

  PERFORM IMPRIMIR_DANFE USING RETURN_CODE US_SCREEN
                      CHANGING OTFDATA.

  CALL FUNCTION 'ZSMARTFORMS_PDF_PREVIEW'
    EXPORTING
      I_OTF                    = OTFDATA
    EXCEPTIONS
      CONVERT_OTF_TO_PDF_ERROR = 1
      CNTL_ERROR               = 2
      OTHERS                   = 3.

ENDFORM.

FORM ENTRY2 USING RETURN_CODE US_SCREEN P_NAST TYPE NAST
         CHANGING OTFDATA TYPE TSFOTF.

  MOVE-CORRESPONDING P_NAST TO NAST.

  PERFORM IMPRIMIR_DANFE USING RETURN_CODE US_SCREEN CHANGING OTFDATA.

ENDFORM.

FORM IMPRIMIR_DANFE USING RETURN_CODE
                          US_SCREEN
                 CHANGING OTFDATA  TYPE TSFOTF.

  DATA: L_DOCNUM  TYPE J_1BNFDOC-DOCNUM, "Cabeçalho da nota fiscal
        L_DOC_FAT TYPE J_1BNFLIN-REFKEY.

  DATA: LW_J1BNFDOC TYPE J_1BNFDOC.

  CLEAR: RETURN_CODE, LW_J1BNFDOC.

  CHECK NAST-KAPPL EQ 'NF'. "Aplicação para condições de mensagens

  MOVE NAST-OBJKY TO L_DOCNUM.

  PERFORM ZF_PARAMETROS_SMARTFORM USING US_SCREEN.

  SELECT SINGLE *
     FROM J_1BNFDOC INTO LW_J1BNFDOC
    WHERE DOCNUM = L_DOCNUM.

  CHECK SY-SUBRC = 0.

* Verifica a nota esta estornada
  "IF LW_J1BNFDOC-CANCEL = 'X'.
  "  MESSAGE E001.
  "ENDIF.

  PERFORM IMPRESSSAO_DANFE USING US_SCREEN
                        CHANGING LW_J1BNFDOC
                                 RETURN_CODE
                                 OTFDATA.

ENDFORM.

FORM ZF_PARAMETROS_SMARTFORM USING P_SCREEN.

  DATA L_REPID TYPE SY-REPID.
  DATA PE_RETURNCODE TYPE  SYSUBRC.
  DATA PE_ITCPO TYPE  ITCPO.
  DATA PE_DEVICE TYPE  TDDEVICE.
  DATA PE_RECIPIENT TYPE  SWOTOBJID.
  DATA PE_SENDER TYPE  SWOTOBJID.

  CLEAR G_CONTROL_PARAMETERS.
  CLEAR G_OUTPUT_OPTIONS.

  L_REPID = SY-REPID.

  CALL FUNCTION 'WFMC_PREPARE_SMART_FORM'
    EXPORTING
      PI_NAST       = NAST
      PI_REPID      = ' '
      PI_SCREEN     = P_SCREEN
    IMPORTING
      PE_RETURNCODE = PE_RETURNCODE
      PE_ITCPO      = PE_ITCPO
      PE_DEVICE     = PE_DEVICE
      PE_RECIPIENT  = PE_RECIPIENT
      PE_SENDER     = PE_SENDER.

  IF PE_RETURNCODE = 0.
    MOVE-CORRESPONDING PE_ITCPO TO G_OUTPUT_OPTIONS.
    G_OUTPUT_OPTIONS-TDNEWID       = 'X'.
    G_OUTPUT_OPTIONS-TDDELETE      = 'X'.

    G_CONTROL_PARAMETERS-DEVICE    = PE_DEVICE.
    G_CONTROL_PARAMETERS-PREVIEW   = P_SCREEN.
    G_CONTROL_PARAMETERS-GETOTF    = PE_ITCPO-TDGETOTF.
    G_CONTROL_PARAMETERS-LANGU     = NAST-SPRAS.
    G_CONTROL_PARAMETERS-NO_DIALOG = 'X'.
  ENDIF.

ENDFORM.                    " ZF_PARAMETROS_SMARTFORM

FORM IMPRESSSAO_DANFE USING P_SCREEN
                   CHANGING P_J1BNFDOC TYPE J_1BNFDOC
                            P_RETURN_CODE
                            OTFDATA TYPE TSFOTF.

  DATA: ST_JOB_OUTPUT_INFO TYPE SSFCRESCL.

  TRY .
      ZESZCL_DOC_ELETRONICO=>ZESZIF_DOC_ELETRONICO~GET_INSTANCE( I_DOCNUM =  P_J1BNFDOC-DOCNUM
        )->SET_REGISTRO( EXPORTING I_DOCNUM       =  P_J1BNFDOC-DOCNUM
                                   I_SEM_BLOQUEIO = ABAP_TRUE
        )->GET_CK_AUTORIZADO_USO(
        )->GET_XML_GRC( IMPORTING E_XML_STRING = DATA(_XML_DOC) ).
    CATCH ZESZCX_DOC_ELETRONICO INTO DATA(EX_DOC).    " .
      EX_DOC->PUBLISHED_ERRO( EXPORTING I_MSGTY = 'S' I_MSGTY_DISPLAY = 'W' ).
      RETURN.
  ENDTRY.

  CHECK _XML_DOC IS NOT INITIAL.

  CLEAR: ST_JOB_OUTPUT_INFO.

* Impressão DANFE
  CALL FUNCTION 'ZESZBRNFE_DANFE'
    EXPORTING
      I_XML                = _XML_DOC
      I_CONTROL_PARAMETERS = G_CONTROL_PARAMETERS
      I_OUTPUT_OPTIONS     = G_OUTPUT_OPTIONS
*     I_VALIDA             = 'X'
    IMPORTING
      E_JOB_OUTPUT_INFO    = ST_JOB_OUTPUT_INFO
    EXCEPTIONS
      NFE_NAO_APROVADA     = 1
      OTHERS               = 2.

  MOVE ST_JOB_OUTPUT_INFO-OTFDATA TO OTFDATA.

  IF SY-SUBRC <> 0.
    CHECK P_SCREEN IS INITIAL.
    P_RETURN_CODE = 4.
    CALL FUNCTION 'NAST_PROTOCOL_UPDATE'
      EXPORTING
        MSG_ARBGB = SY-MSGID
        MSG_NR    = SY-MSGNO
        MSG_TY    = SY-MSGTY
        MSG_V1    = SY-MSGV1
        MSG_V2    = SY-MSGV2
        MSG_V3    = SY-MSGV3
        MSG_V4    = SY-MSGV4
      EXCEPTIONS
        OTHERS    = 1.

  ENDIF.

ENDFORM.
